import org.gradle.api.plugins.quality.CheckstyleExtension
import org.gradle.api.plugins.quality.CodeNarcExtension

plugins {
    id 'checkstyle'
    id 'codenarc'
}

// Resolved relative to the root project directory, which is the parent of build-logic/.
def codeStyleConfigDir = rootProject.file('build-logic/config')
def checkstyleConfigDir = new File(codeStyleConfigDir, 'checkstyle')
def codenarcConfigDir = new File(codeStyleConfigDir, 'codenarc')

extensions.configure(CheckstyleExtension) {
    it.toolVersion = checkstyleVersion
    it.configDirectory = checkstyleConfigDir
    it.maxWarnings = 0
    it.showViolations = true
    it.ignoreFailures = false
}

tasks.withType(Checkstyle).configureEach {
    group = 'verification'
    onlyIf { !project.hasProperty('skipCodeStyle') }
}

extensions.configure(CodeNarcExtension) {
    it.toolVersion = codenarcVersion
    it.configFile = new File(codenarcConfigDir, 'codenarc.groovy')
    it.maxPriority1Violations = 0
    it.maxPriority2Violations = 0
    it.maxPriority3Violations = 0
}

tasks.withType(CodeNarc).configureEach {
    group = 'verification'
    onlyIf { !project.hasProperty('skipCodeStyle') }
}

tasks.register('codeStyle') {
    group = 'verification'
    description = 'Runs all code style checks (Checkstyle + CodeNarc).'
    dependsOn tasks.withType(Checkstyle)
    dependsOn tasks.withType(CodeNarc)
}
