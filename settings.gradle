import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        if (System.getenv('INCLUDE_MAVEN_LOCAL')) {
            mavenLocal()
        }
        maven { url = 'https://repo.grails.org/grails/restricted' }
    }

    includeBuild('./build-logic') {
        it.name = 'build-logic'
    }
}

plugins {
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.4.0'
}

def isCI = System.getenv().containsKey('CI')
def isLocal = !isCI
def isReproducibleBuild = System.getenv('SOURCE_DATE_EPOCH') != null
if (isReproducibleBuild) {
    gradle.settingsEvaluated {
        logger.warn('*************** Remote Build Cache Disabled due to Reproducible Build ********************')
        logger.warn("Build date will be set to (SOURCE_DATE_EPOCH=${System.getenv("SOURCE_DATE_EPOCH")})")
    }
}

buildCache {
    local { enabled = (isLocal && !isReproducibleBuild) || (isCI && isReproducibleBuild) }
}

rootProject.name = 'grails-server-timing-root'

include 'plugin'
project(':plugin').name = 'grails-server-timing'
include 'docs'
project(':docs').name = 'grails-server-timing-docs'
include 'coverage'

def examples = file('examples').list()
examples.each { example ->
    include example
    project(":$example").projectDir = file("examples/$example")
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    repositories {
        if (System.getenv('INCLUDE_MAVEN_LOCAL')) {
            mavenLocal()
        }
        maven { url = 'https://repo.grails.org/grails/restricted' }
    }
}
